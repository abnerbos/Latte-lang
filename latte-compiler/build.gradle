def VERSION = System.getenv('LATTE_VERSION')

group 'org.latte-lang'
version VERSION

buildscript {
    def VERSION_ = System.getenv('LATTE_VERSION')

    repositories {
        mavenLocal()
    }
    dependencies {
        classpath 'org.latte-lang:latte-class-recorder:' + VERSION_
    }
}

apply plugin: 'java'
apply plugin: 'org.latte-lang.class-recorder'
apply plugin: 'maven'
apply plugin: LatteBuild

recordConfig {
    directory = 'classes/main'
    file = 'classes.txt'
}

sourceCompatibility = 1.6
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

test {
    useJUnit()
    exclude 'lt/**'
}

uploadArchives {
    repositories {
        mavenDeployer {
            def deploy_user = System.getenv('DEPLOY_USER')
            def deploy_pass = System.getenv('DEPLOY_PASS')

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: deploy_user, password: deploy_pass)
            }
        }
    }
}

class LatteBuild implements Plugin<Project> {
    @Override
    void apply(Project project) {
        def latteBuild = project.task('latteBuild')
        project.tasks['install'].dependsOn project.tasks['test']
        latteBuild.dependsOn project.tasks['install']

        def latteTest = project.task('latteTest')
        latteTest.dependsOn project.tasks['test']

        def latteDeploy = project.task('latteDeploy')
        latteDeploy.dependsOn project.tasks['uploadArchives']
    }
}
